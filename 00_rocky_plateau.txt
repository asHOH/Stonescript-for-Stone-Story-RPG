/* --------- Level 0: Rocky plateau --------- */

/* ----------------- imports ---------------- */
var eq = import UserScript/eq
var util = import UserScript/util
var s = import UserScript/s
var hud = import UserScript/hud

/* ---------------- variables --------------- */
var permapot = true
var mask_run = permapot
var post_mask_run = false
var timer = new UserScript/timer
var boss_atk_counter = new UserScript/timer
var is_stunned = false

?loc = "rocky"
  /* ------------- set parameters ------------- */
  util.harvest_on = false
  util.autowalk_on = false
  ?mask_run
    util.cultist_walk_on = true
  ?permapot & (s.loop_count > 1)
    mask_run = false
    post_mask_run = true

  /* --------------- strategies --------------- */
  brew_potion() // brew lucky potion if permapot is on, otherwise berserk
  check_stunned() // Normally you should not be stunned. This just helps debugging.
  timer.update()

  ?s.loc_begin_or_loop
    boss_atk_counter.set(3)
  ?!timer.between(0,1) & foe.distance > 16
    util.walk()
  :?timer.between(0,1) | (!util.try_dashing()) // timer 2 is the dodge moment. wait 2 frames then dash back.
    ?foe.distance < 11
      ?foe = "phase1"
        phase1()
      :?foe = "phase2"
        phase2()
      :?foe = "phase3"
        phase3()
      :
        scout()

/* Phase 1: weakness element is fire. Apply debuffs.
  Phase 2: Prepare weapons of every element. When it uses vigor attack, it resist debuffs.
  phase 3: Mind stone when it shields or uses orbital laser. Weak to all elements but gains resistance buff each time it shields. */

func phase3()
  inspect_weakness_phase3()
  >`0,17,boss_atk_counter@boss_atk_counter.t@
  // determine: when to dodge
  ?(!s.can_activate_or_use_bardiche) & (!permapot)
    s.activate_potion()
  ?foe.state = 115 // satellite
    ?foe.state_changed
      timer.set(79)
    boss_atk_counter.set(3)
  :?foe.state = 32
    ?foe.state_changed
      boss_atk_counter.update()
    :?!boss_atk_counter.at(1)
      ?defend_at(89)
        return
    :?boss_atk_counter.at(1) & foe.state_at(32, 68)
      util.try_dodge()
      timer.set(2) // you'll be stunned on foe state 33,0 ~ 33,1
      return
  
  // action: dodge or attack
  ?timer.at(2)
    util.try_dodge()
  :?mask_run
    eq.R_cultist()
    ?!L_debuff_weapon()
      eq.L_sword()
  :?permapot | !util.try_bardiche()
    ?s.foe_has_armor
      eq.L(eq.best_hammer)
      R_debuff_weapon()
    :
      eq.L_sword()
      inspect_another_weakness_phase3()
      ?!R_debuff_weapon()
        eq.R_sword()
    

func phase1()
  eq.element = "fire"
  ?mask_run
    eq.R_cultist()
    ?!L_debuff_weapon()
      eq.L_sword()
  :
    ?!permapot & util.try_bardiche()
      return
    :
      eq.L_sword()
      ?!R_debuff_weapon()
        eq.R_sword2()
      
  defend_at(64)
  check_retreat()


func phase2()
  util.inspect_weakness()
  eq.L_sword()
  ?mask_run
    eq.R_cultist()
  :
    ?(foe.buffs.string = "buff") | !R_debuff_weapon()
      eq.R_sword2()
  
  defend_at(59)
  check_retreat()

func scout()
  eq.element = "none"
  ?mask_run
    eq.R_cultist()
    ?!L_debuff_weapon()
      eq.L_sword()
  :
    ?!permapot & util.try_bardiche()
      return
    :
      eq.L(eq.best_sword)
      ?!R_debuff_weapon()
        eq.R_sword()

  defend_at(54)

func L_debuff_weapon()
  ?s.foe_debuff_F_time < 5
    eq.L_sword_dF()
    return true
  ?s.foe_debuff_I_count < 6 | s.foe_debuff_I_time < 6
    eq.L_sword_dI()
    return true
  ?(!s.foe_debuff_P) & (foe ! "phase3" | (foe = "phase3" & foe.state = 32 & foe.time >= 55))
    eq.L_sword_dP()
    return true
  ?s.lost_hp >= 60
    eq.L_sword_dL()
    return true
  ?s.foe_has_armor
    eq.L(eq.best_hammer2)
    return true
  ?s.lost_hp >= 15
    eq.L_sword_dL()
    return true
  return false

func R_debuff_weapon()
  ?s.foe_debuff_F_time < 5
    eq.R_sword_dF()
    return true
  ?s.foe_debuff_I_count < 6 | s.foe_debuff_I_time < 6
    eq.R_sword_dI()
    return true
  ?(!s.foe_debuff_P) & (foe ! "phase3" | (foe = "phase3" & foe.state = 32 & foe.time >= 55))
    eq.R_sword_dP()
    return true
  ?s.lost_hp >= 60
    eq.R_sword_dL()
    return true
  ?s.foe_has_armor
    eq.R(eq.best_hammer2)
    return true
  ?s.lost_hp >= 20
    eq.R_sword_dL()
    return true
  return false

func inspect_weakness_phase3()
  // ∞>❄>φ>*>♥>∞
  ?foe.buffs.string ! "ice"
    eq.element = "ice"
  :?foe.buffs.string ! "fire"
    eq.element = "fire"
  :?foe.buffs.string ! "aether"
    eq.element = "aether"
  :?foe.buffs.string ! "vigor"
    eq.element = "vigor"
  :
    eq.element = "poison"
  return eq.element

func inspect_another_weakness_phase3()
  // ∞>❄>φ>*>♥>∞
  ?foe.buffs.string ! "ice" & eq.element ! "ice"
    eq.element = "ice"
  :?foe.buffs.string ! "fire" & eq.element ! "fire"
    eq.element = "fire"
  :?foe.buffs.string ! "aether" & eq.element ! "aether"
    eq.element = "aether"
  :?foe.buffs.string ! "vigor" & eq.element ! "vigor"
    eq.element = "vigor"
  :
    eq.element = "poison"
  return eq.element

func defend()
  ?s.lost_hp >= 2
    eq.R_shield_ah()
  :
    eq.R_shield_af()
  >`0,0,defend!

func defend_at(foe_time)
  ?s.foe_state_at(32, foe_time)
    defend()
    return true
  return false

func check_retreat()
  ?foe.count > 0 & foe.hp <= 0 & !mask_run
    eq.R_compound_auto(false)
    equipL ouroboros

func brew_potion()
  ?permapot
    s.brew_potion("lucky")
    s.activate_potion()
  :
    s.brew_potion("berserk")

func check_stunned()
  ?debuffs.string = "stun" & !is_stunned
    hud.perma_show(0, 12, "Stunned at timer" + timer.t + ", fs" + foe.state + ", " + foe.time + ", totaltime" + s.total_time)
    is_stunned = true
