/* ----- System variables and functions ----- */

/* ---------------- functions --------------- */
var cd = item.GetCooldown
var floor = math.FloorToInt


/* ---------------- const var --------------- */
var is_first_trial = (loc.bestTime = -1)
var initial_best_time = loc.bestTime

/* -------------- loop-wise var ------------- */
var hp_last_loop = maxhp
var hp_lost_total = 0
var best_time = initial_best_time
var avg_time = loc.averageTime
var new_best = false
var current_potion = item.potion

func on_loop()
  hp_lost_total += math.Max(hp_last_loop - hp_last_frame, 0)
  hp_last_loop = hp_
  best_time = loc.bestTime
  avg_time = loc.averageTime
  new_best = (best_time < initial_best_time)
  // current_potion = item.potion


/* ------------- frame-wise var ------------- */
var ai_enabled = false
var is_walking = false
var can_activate_item = false
var can_activate_or_use_bardiche = false
var has_pickup = false
var relative_x = 0

var hp_ = maxhp
var hp_last_frame = maxhp
var lost_hp = 0
var armor_ = 0.0
var debuffs_chill = false

var all_foe_count = 0
var cur_screen_foe_count = 0
var foe_hp_and_armor = 0
var foe_has_armor = false
var foe_maxarmor = 0
var foe_state_last_frame = 0
var foe_state = 0
var foe_state_changed = false
var foe_debuff_f = false
var foe_debuff_F = false
var foe_debuff_P = false
var foe_debuff_I = false
var foe_debuff_I_count = 0
var foe_debuff_I_time = 0
var foe_debuff_F_time = 0
var foe_debuff_P_time = 0


var loop_count = 1
var total_time_last_frame = 0
var total_time = 0
var true_total_time = 0
var loc_begin_or_loop = true


func on_start_frame()
  disable npcDialog
  ai_enabled = ai.enabled & !(ai.paused)
  is_walking = ai.walking
  can_activate_item = item.CanActivate()
  can_activate_or_use_bardiche = (cd("bardiche") <= 0 | cd("bardiche") > 878) & ai_enabled & can_activate_item
  has_pickup = pickup.distance < 10
  relative_x = pos.x - screen.x

  hp_last_frame = hp_
  hp_ = hp
  lost_hp = maxhp - hp_
  armor_ = relu(armor + armor.f / 10.0)
  debuffs_chill = (debuffs.string = "chill:")
  all_foe_count = foe.GetCount(200)
  cur_screen_foe_count = foe.GetCount(36 - relative_x)
  foe_hp_and_armor = foe.hp + foe.armor
  foe_has_armor = foe.armor > 0
  foe_maxarmor = math.Max(foe.armor, foe.maxarmor)
  foe_state_last_frame = foe_state
  foe_state = foe.state
  foe_state_changed = (foe_state ! foe_state_last_frame)
  foe_debuff_f = (foe.debuffs.string = "dot_2:")
  foe_debuff_F = (foe.debuffs.string = "dot:")
  foe_debuff_P = (foe.debuffs.string = "damage:")
  foe_debuff_I = (foe.debuffs.string = "chill:")

  ?foe_debuff_I
    foe_debuff_I_count = int.Parse(string.Break(string.Split(foe.debuffs.string, "chill:")[1], 1)[0])
    foe_debuff_I_time = int.Parse(string.Split(string.Split(string.Split(foe.debuffs.string, "chill:")[1], ":")[1], ",")[0])
  :
    foe_debuff_I_count = 0
    foe_debuff_I_time = 0
  ?foe_debuff_F
    foe_debuff_F_time = int.Parse(string.Split(string.Split(string.Split(foe.debuffs.string, "dot:")[1], ":")[1], ",")[0])
  :
    foe_debuff_F_time = 0
  ?foe_debuff_P
    foe_debuff_P_time = int.Parse(string.Split(string.Split(string.Split(foe.debuffs.string, "damage:")[1], ":")[1], ",")[0])
  :
    foe_debuff_P_time = 0

  total_time_last_frame = total_time
  total_time = totaltime
  ?total_time > total_time_last_frame
    true_total_time += total_time - total_time_last_frame  //make up for the skills such as blade
  :
    true_total_time++
  loc_begin_or_loop = (total_time = 1)

  ?loc.loop
    on_loop()
    loop_count++

/* ----------- in-combat functions ---------- */
func can_activate_now(item_name)
  return cd(item_name) <= 0 & ai_enabled & can_activate_item

func foe_state_at(s, t)
  return foe_state = s & foe.time = t

func foe_state_between(s, t1, t2)
  return foe_state = s & foe.time >= t1 & foe.time <= t2

func brew_potion(potion_name)
  ?!loc_begin_or_loop
    return false
  ?potion_name = "experience"
    brew wood
  :?potion_name = "strength"
    brew stone
  :?potion_name = "healing"
    brew tar
  :?potion_name = "lightning"
    brew bronze
  :?potion_name = "defensive"
    brew tar + stone
  :?potion_name = "cleansing"
    brew tar + wood
  :?potion_name = "vampiric"
    brew tar + bronze
  :?potion_name = "invisibility"
    brew stone + wood
  :?potion_name = "lucky"
    brew stone + bronze
  :?potion_name = "berserk"
    brew wood + bronze
  :// if invalid potion is brewed you should get a warning here
  current_potion = potion_name
  return true

func activate_potion()
  ?current_potion ! "empty" & can_activate_item
    activate potion
    current_potion = "empty"
    return true
  return false


/* ------------ string functions ------------ */
func pad_left(str, l)
  str = str + ""
  var pad_len = l - string.Size(str)
  var pad = str_repeat(" ", pad_len)
  return pad + str


func pad_right(str, l)
  str = str + ""
  var pad_len = l - string.Size(str)
  var pad = str_repeat(" ", pad_len)
  return str + pad

func str_repeat(str, n)
  ?n < 1
    return ""
  var result = ""
  n = math.Min(floor(n), 100)
  for i = 1..n
    result += str
  return result


/* ------------- Other functions ------------ */
func do_nothing(x)
  return x

func create_ui(ui_, x, y, w)
  ui_.x = x
  ui_.y = y
  ui_.w = w
  ui_.dock = top_left
  ui_.anchor = top_left
  return ui_

func relu(num)
  return math.Max(num, 0)

func create_healthbar(start_text, ticks, current, cap, color_min, color_max, color_BG, end_text)
  current = math.Max(current, 1)
  var healthbar = []
  var full_ticks = (0.0+current-1)/cap*ticks + 0.5
  var has_half_tick = math.RoundToInt(full_ticks - floor(full_ticks))
  var half_tick = str_repeat("▒", has_half_tick)
  var color_current = color.Lerp(color_min, color_max, (0.0+current)/cap)
  healthbar.Add([color_BG, start_text])
  healthbar.Add([color_current, str_repeat("█", floor(full_ticks)) + half_tick, ticks])
  healthbar.Add([color_BG, end_text])
  return healthbar
