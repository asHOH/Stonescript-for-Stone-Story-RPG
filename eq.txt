/*
Holds all possible equipment sets for fighting
*/

/* ----------------- imports ---------------- */
var s = import UserScript/s

/* ---------------- variables --------------- */
var element = "none" // target element of equipment
var eq_type = "none" // current type of equipment set
var left_hand = "" // current left hand equipment
var right_hand = "" // current right hand equipment
var universal_sword_in_use = 0

/* ----------------- shields ---------------- */
var compound_shield_regen = "compound shield *10 shiny"
var compound_shield_regen_armor = 13
var compound_shield_maxarmor = "compound shield *10 -shiny"
var compound_shield_maxarmor_armor = 24

var poison_shield_A = "poison shield 10* A"
var ice_shield_A = "ice shield 10* A"
var fire_shield_A = "fire shield 10* A"
var aether_shield_A = "" // "aether shield 10* A"
var vigor_shield_A = "vigor shield 10* A"

var shield_ah = "vigor shield 10* ah" // heal when hit
var shield_ap = "poison shield 10* ap" // bonus damage when hit
var shield_af = "fire shield 10* af" // burning damage when hit
var shield_ai = "ice shield 10* ai" // slow when hit

/* ----------------- swords ----------------- */
var universal_sword = "vigor sword 10* dL -big" // universal sword for when you don't have a good sword
var universal_sword2 = "ice sword 10* dI -big" // backup universal sword
var best_sword = "ice sword 10* +17 D -big" // best DPS sword
var best_sword2 = "fire sword 10* +17 D -big" // second best DPS sword

var poison_sword = "poison sword 10* D -big"
var ice_sword = "ice sword 10* D -big"
var fire_sword = "fire sword 10* D -big"
var aether_sword = "aether sword 10* D -big"
var vigor_sword = "vigor sword 10* D -big"

var poison_sword2 = "poison sword 10* +9 D -big"
var ice_sword2 = "ice sword 10* +9 D -big"
var fire_sword2 = "fire sword 10* +13 D -big"
var aether_sword2 = "aether sword 10* +9 D -big"
var vigor_sword2 = "vigor sword 10* +9 D -big"

var sword_dL = "vigor sword 10* dL -big"
var sword_dP = "poison sword 10* dP -big"
var sword_dI = "ice sword 10* dI -big"
var sword_dF = "fire sword 10* dF -big"

/* ----------------- hammers ---------------- */
var best_hammer = "poison hammer 10* D" // best DPS hammer
var best_hammer2 = "fire hammer 10* D" // second best DPS hammer

var poison_hammer = "poison hammer 10* D"
var ice_hammer = "" // "ice hammer 10* D"
var fire_hammer = "fire hammer 10* D"
var aether_hammer = "" // "aether hammer 10* D"
var vigor_hammer = "" // "vigor hammer 10* D"

/* ----------------- others ----------------- */
var repeating_crossbow = "repeating crossbow 10*"
var aether_crossbow = "aether crossbow 10*"
var wand_dI = "ice wand 10* dI"
var wand_dP = "poison wand 10* dP"

func on_start_frame()
  universal_sword_in_use = 0
  left_hand = ""
  right_hand = ""

func set_eq_type(type)
  eq_type = type

func L(equipment)
  ?equipment & !(s.str_equals(equipment, right_hand))
    left_hand = equipment
  :?!(s.str_equals(equipment, universal_sword))
    left_hand = universal_sword
  :
    left_hand = universal_sword2
  equipL @left_hand@


func R(equipment)
  ?equipment & !(s.str_equals(equipment, left_hand))
    right_hand = equipment
  :?!(s.str_equals(equipment, universal_sword))
    right_hand = universal_sword
  :
    right_hand = universal_sword2
  equipR @right_hand@


/* -------------- normal shield ------------- */
// shields are seldom used in combat (most used when there is no foe)
func R_shield_A()
  ?element = "poison"
    R(poison_shield_A)
  :?element = "fire"
    R(fire_shield_A)
  :
    R_shield_ah()

func R_shield_maxarmor()
  R(compound_shield_maxarmor)

func shield_maxarmor()
  set_eq_type("maxarmor")
  R_shield_maxarmor()
  L("quest")

func R_shield_regen()
  R(compound_shield_regen)

func shield_regen()
  set_eq_type("max_regen")
  R_shield_regen()
  L("quest")

func R_shield_auto(quest_equipped)
  ?s.armor_ <= compound_shield_regen_armor | ((s.armor_ <= compound_shield_regen_armor + 2) & quest_equipped)
    R_shield_regen()
    return true
  :?s.armor_ <= compound_shield_maxarmor_armor | ((s.armor_ <= compound_shield_maxarmor_armor + 2) & quest_equipped)
    R_shield_maxarmor()
    return true
  return false



/* -------------- debuff shield ------------- */
func R_shield_ah()
  R(shield_ah)

func R_shield_ap()
  R(shield_ap)

func R_shield_af()
  R(shield_af)

func R_shield_ai()
  R(shield_ai)


/* ---------------- dps sword --------------- */
func L_sword()
  ?element = "poison"
    L(poison_sword)
  :?element = "ice"
    L(ice_sword)
  :?element = "fire"
    L(fire_sword)
  :?element = "aether"
    L(aether_sword)
  :?element = "vigor"
    L(vigor_sword)
  :
    L(best_sword)

func R_sword()
  ?element = "poison"
    R(poison_sword)
  :?element = "ice"
    R(ice_sword)
  :?element = "fire"
    R(fire_sword)
  :?element = "aether"
    R(aether_sword)
  :?element = "vigor"
    R(vigor_sword)
  :
    R(best_sword)

func L_sword2()
  ?element = "poison"
    L(poison_sword2)
  :?element = "ice"
    L(ice_sword2)
  :?element = "fire"
    L(fire_sword2)
  :?element = "aether"
    L(aether_sword2)
  :?element = "vigor"
    L(vigor_sword2)
  :
    L(best_sword2)

func R_sword2()
  ?element = "poison"
    R(poison_sword2)
  :?element = "ice"
    R(ice_sword2)
  :?element = "fire"
    R(fire_sword2)
  :?element = "aether"
    R(aether_sword2)
  :?element = "vigor"
    R(vigor_sword2)
  :
    R(best_sword2)

/* -------------- debuff sword -------------- */
func L_sword_dL()
  L(sword_dL)

func R_sword_dL()
  R(sword_dL)

func L_sword_dF()
  L(sword_dF)

func R_sword_dF()
  R(sword_dF)

func L_sword_dP()
  L(sword_dP)

func R_sword_dP()
  R(sword_dP)

func L_sword_dI()
  L(sword_dI)

func R_sword_dI()
  R(sword_dI)

/* ---------------- hammer --------------- */
func L_hammer()
  ?element = "poison"
    L(poison_hammer)
  :?element = "fire"
    L(fire_hammer)
  :?element = "aether"
    L(aether_hammer)
  :?element = "vigor"
    L(vigor_hammer)
  :?element = "ice"
    L(ice_hammer)
  :
    L_sword()

func R_hammer()
  ?element = "poison"
    R(poison_hammer)
  :?element = "fire"
    R(fire_hammer)
  :?element = "aether"
    R(aether_hammer)
  :?element = "vigor"
    R(vigor_hammer)
  :?element = "ice"
    R(ice_hammer)
  :
    R_sword()

/* ---------------- crossbow --------------- */

func crossbow()
  ?element = "aether"
    equip @aether_crossbow@
  :
    equip @repeating_crossbow@

/* ------------------ melee ----------------- */
// double melee
func mmelee()
  set_eq_type("mmelee")
  L_sword()
  R_sword2()

// double splashing melee
func mmelee_spl()
  rranged_spl() // big sword is bad...
  set_eq_type("mmelee_spl")

// piercing melee, i.e. Hammers. range = 5.
func melee_prc()
  set_eq_type("melee_prc")
  L_hammer()
  R_sword()

/* ----------------- ranged ----------------- */
// double ranged
func rranged()
  set_eq_type("rranged")
  // repeating crossbow is the best non-splashing ranged weapon in early game
  equip @repeating_crossbow@

// double splashing ranged
func rranged_spl()
  set_eq_type("rranged_spl") // under development
  equipL vigor wand *10
  equipR poison wand *10 dP

// double debuff ranged
func ddanged()
  set_eq_type("ddanged_spl")
  L(wand_dI)
  R(wand_dP)

/* ------------- armor generator ------------ */
// func armor_gen()
//   set_eq_type("armor_gen")
//   ?element = "poison"
//     equipL poison hammer *10 a
//     equipR poison shield *10 A

/* ------------ specific weapons ------------ */
func blade()
  set_eq_type("blade!!")
  equip blade
  activate R

func blade_no_R()
  set_eq_type("blade")
  equip blade

func bardiche(use)
  equip bardiche *10
  ?use
    set_eq_type("bardiche!")
    ?s.can_activate_or_using_bardiche
      set_eq_type("bardiche!!")
      activate R
  :
    set_eq_type("bardiche")

func repeating()
  set_eq_type("repeating")
  equip @repeating_crossbow@

func pick_up()
  set_eq_type("pickup")
  equipL star
  equipR triskelion

func R_fire_talisman()
  set_eq_type("fire_talisman")
  equipR fire talisman
  activate R
  // equipL aether talisman
  // activate L

func R_aether_talisman()
  set_eq_type("aether_talisman")
  equipR aether talisman
  activate R

/* ------------- dash and dodge ------------- */
func dashing()
  set_eq_type("dashing")
  equipR dashing shield
  L_sword()


func quarter_dash()
  set_eq_type("quarterstaff")
  equip quarterstaff
  activate R


func dodge()
  set_eq_type("dodge")
  equipL mind
  equipR vigor shield *10 A


func R_cultist()
  R("cultist")